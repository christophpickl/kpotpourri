//_                _                               _
//| | ___ __   ___ | |_ _ __   ___  _   _ _ __ _ __(_)
//| |/ / '_ \ / _ \| __| '_ \ / _ \| | | | '__| '__| |
//|   <| |_) | (_) | |_| |_) | (_) | |_| | |  | |  | |
//|_|\_\ .__/ \___/ \__| .__/ \___/ \__,_|_|  |_|  |_|
//|_|             |_|

// ./gradlew install -Dkpotpourri.version=1.X-SNAPSHOT

buildscript {
    ext.myVersion = System.getProperty("kpotpourri.version", "SNAPSHOT")
    apply from: "$rootDir/gradle/dependency_versions.gradle"

    repositories {
        mavenCentral()
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "https://dl.bintray.com/kotlin/kotlin-eap/" }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$version_kotlin"
        classpath "org.jetbrains.kotlin:kotlin-noarg:$version_kotlin"
        classpath "org.jetbrains.kotlin:kotlin-allopen:$version_kotlin"
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:$version_bintray"
//        classpath "com.autoscout24.gradle:gradle-todo-plugin:$version_todo"
        classpath "org.kt3k.gradle.plugin:coveralls-gradle-plugin:$version_coveralls"
        classpath "org.standardout:gradle-versioneye-plugin:$version_versioneye"
    }
}

apply plugin: 'org.standardout.versioneye'
versioneye {
    includeSubProjects = true
}


allprojects {
    group = 'com.github.christophpickl.kpotpourri'
    version = myVersion
    repositories {
        jcenter()
        mavenCentral()
    }

    // https://discuss.gradle.org/t/travis-ci-org-gradle-launcher-daemon-client-daemondisappearedexception-gradle-build-daemon-disappeared-unexpectedly-it-may-have-been-killed-or-may-have-crashed/13106/3
    if (System.env.TRAVIS == 'true') {
        tasks.withType(GroovyCompile) {
            groovyOptions.fork = false
        }
        tasks.withType(Test) {
            // containers (currently) have 2 dedicated cores and 4GB of memory
            maxParallelForks = 1
            minHeapSize = '128m'
        }
    }
}


subprojects { subproject ->
    apply plugin: "kotlin"
    apply plugin: "kotlin-noarg"
    apply plugin: "java"
    // apply plugin: 'com.autoscout24.gradle.todo'
    apply from: "$rootDir/gradle/test.gradle"
    apply from: "$rootDir/gradle/publishing.gradle"

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$version_kotlin"
        compile "org.jetbrains.kotlin:kotlin-reflect:$version_kotlin"


        compile "io.github.microutils:kotlin-logging:" + version_kotlin_logging
        runtime "org.slf4j:jcl-over-slf4j:" + version_jcl
        compile "com.google.guava:guava:" + version_guava

        if (subproject.name != "test4k") {
            testCompile project(':test4k')
        }
        testRuntime "ch.qos.logback:logback-classic:" + version_logback
    }

    sourceCompatibility = versionJava
    targetCompatibility = versionJava
    compileKotlin {
        kotlinOptions.jvmTarget = versionJava.toString()
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = versionJava.toString()
    }

    noArg {
        annotation("com.github.christophpickl.kpotpourri.common.KotlinNoArg")
    }

//    todo {
//        todoPattern = "//[\\t\\s]*(FIXME|TODO) (.*)"
//        failIfFound = true
//        sourceFolder = "${subproject.name}/src" // in order to look into main and test folders
//        fileExtensions = ["kt"]
//    }

    // execute "gradle listDependencies" on root project, dumps dependencies for all submodules (hack needed in gradle)
    task listDependencies(type: DependencyReportTask) {}

    jar {
        manifest {
            attributes 'Implementation-Title': 'kpotpourri - ${name}',
                    'Implementation-Vendor': 'Christoph Pickl',
                    'Implementation-Version': myVersion
        }
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task testJar(type: Jar) {
        classifier = 'tests'
        from sourceSets.test.output
    }

    task testSourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'testSources'
        from sourceSets.test.allSource
    }

    artifacts {
        archives sourcesJar
        archives testJar
        archives testSourcesJar
    }
}
